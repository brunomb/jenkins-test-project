apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'

// Add checkstyle, findbugs, pmd and lint to the check task.
check.dependsOn 'checkstyle', 'findbugs', 'pmd', 'lint'

task checkstyle(type: Checkstyle) {
    ignoreFailures = true
    configFile file("${project.rootDir}/config/quality/checkstyle/checkstyle.xml")
    configProperties.checkstyleSuppressionsPath = file("${project.rootDir}/config/quality/checkstyle/suppressions.xml").absolutePath
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}

task findbugs(type: FindBugs) {
    ignoreFailures = true
    effort = "max"
    reportLevel = "high"
    excludeFilter = new File("${project.rootDir}/config/quality/findbugs/findbugs-filter.xml")
    classes = files("${project.rootDir}/app/build/intermediates/classes")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
    reports {
        xml.enabled = true
        html.enabled = false
        xml {
        	destination "$project.buildDir/reports/findbugs/findbugs.xml"
		xml.withMessages true
	}
        html {
		destination "$project.buildDir/reports/findbugs/findbugs.html"
	}
    }
}

task pmd(type: Pmd) {
    ignoreFailures = true
    ruleSetFiles = files("${project.rootDir}/config/quality/pmd/pmd-ruleset.xml")
    ruleSets = []
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    reports {
        xml.enabled = true
        html.enabled = true
        xml {
		destination "$project.buildDir/reports/pmd/pmd.xml"
	}
        html {
		destination "$project.buildDir/reports/pmd/pmd.html"
	}
    }
}

android {
    lintOptions {
        abortOnError false
        htmlReport true
        xmlReport true
        lintConfig file("${project.rootDir}/config/quality/lint/lint.xml")
        htmlOutput file("$project.buildDir/reports/lint/lint.html")
        xmlOutput file("$project.buildDir/reports/lint/lint.xml")
    }
}

tasks.withType(Checkstyle) {
  reports {
    xml.enabled true
    html.enabled true
    xml.destination rootProject.file("$project.buildDir/reports/checkstyle/checkstyle-result.xml")
    html.destination rootProject.file("$project.buildDir/reports/checkstyle/checkstyle-result.html")
  }
}